#pragma kernel Diffuse
#pragma kernel UpdateVelocity

#pragma multi_compile _ FREE_SURFACE NO_SURFACE

#include "../Common.hlsl"

float4 _DiffusionParameter;

#if defined(FREE_SURFACE)
StructuredBuffer<uint> _GridTypeBufferRead;
#endif

StructuredBuffer<float3> _GridVelocityBufferRead;
RWStructuredBuffer<float3> _GridVelocityBufferWrite;

StructuredBuffer<float3> _GridDiffusionBufferRead;
RWStructuredBuffer<float3> _GridDiffusionBufferWrite;

[numthreads(128, 1, 1)]
void Diffuse(uint3 id : SV_DispatchThreadID)
{
    RETURN_IF_INVALID(id);

    const uint c_id = id.x;
    const uint3 c_index = CellIDToCellIndex(c_id);

    // diffuse using Jacobi iterations
    const uint c_id_xp = CellIndexToCellID(c_index + int3(-1, 0, 0));
    const uint c_id_xn = CellIndexToCellID(c_index + int3(1, 0, 0));
    const uint c_id_yp = CellIndexToCellID(c_index + int3(0, -1, 0));
    const uint c_id_yn = CellIndexToCellID(c_index + int3(0, 1, 0));
    const uint c_id_zp = CellIndexToCellID(c_index + int3(0, 0, -1));
    const uint c_id_zn = CellIndexToCellID(c_index + int3(0, 0, 1));

    float3 velocity = 0;

#if defined(FREE_SURFACE)
    velocity += _DiffusionParameter.x * _GridDiffusionBufferRead[_GridTypeBufferRead[c_id_xp] == GT_FLUID ? c_id_xp : c_id];
    velocity += _DiffusionParameter.x * _GridDiffusionBufferRead[_GridTypeBufferRead[c_id_xn] == GT_FLUID ? c_id_xn : c_id];
    velocity += _DiffusionParameter.y * _GridDiffusionBufferRead[_GridTypeBufferRead[c_id_yp] == GT_FLUID ? c_id_yp : c_id];
    velocity += _DiffusionParameter.y * _GridDiffusionBufferRead[_GridTypeBufferRead[c_id_yn] == GT_FLUID ? c_id_yn : c_id];
    velocity += _DiffusionParameter.z * _GridDiffusionBufferRead[_GridTypeBufferRead[c_id_zp] == GT_FLUID ? c_id_zp : c_id];
    velocity += _DiffusionParameter.z * _GridDiffusionBufferRead[_GridTypeBufferRead[c_id_zn] == GT_FLUID ? c_id_zn : c_id];
    velocity += _DiffusionParameter.w * _GridVelocityBufferRead[c_id];
#else
    velocity += _DiffusionParameter.x * _GridDiffusionBufferRead[c_id_xp];
    velocity += _DiffusionParameter.x * _GridDiffusionBufferRead[c_id_xn];
    velocity += _DiffusionParameter.y * _GridDiffusionBufferRead[c_id_yp];
    velocity += _DiffusionParameter.y * _GridDiffusionBufferRead[c_id_yn];
    velocity += _DiffusionParameter.z * _GridDiffusionBufferRead[c_id_zp];
    velocity += _DiffusionParameter.z * _GridDiffusionBufferRead[c_id_zn];
    velocity += _DiffusionParameter.w * _GridVelocityBufferRead[c_id];
#endif

    _GridDiffusionBufferWrite[c_id] = velocity;
}

[numthreads(128, 1, 1)]
void UpdateVelocity(uint3 id : SV_DispatchThreadID)
{
    RETURN_IF_INVALID(id);

    const uint c_id = id.x;
    const uint3 c_index = CellIDToCellIndex(c_id);

    const float3 velocity = _GridDiffusionBufferRead[c_id];

    _GridVelocityBufferWrite[c_id] = velocity;
}